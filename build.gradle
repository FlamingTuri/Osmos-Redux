apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'idea'
apply plugin: 'application'

version '1.1.0'

mainClassName = 'it.unibo.osmos.redux.main.AppLauncher'
sourceCompatibility = 1.8
scaladoc.destinationDir = file("$docsDir/$scaladocFolder")

sourceSets {
    main {
        resources {
            srcDirs = ["src/main/resources"]
        }
        scala {
            srcDirs = ["src/main/scala"]
        }
    }

    test {
        resources {
            srcDirs = ["src/test/resources"]
        }
        scala {
            srcDirs = ["src/test/scala"]
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.scala-lang', name: 'scala-library', version: '2.12.2'
    compile group: 'org.scala-lang', name: 'scala-compiler', version: '2.12.6'

    compile group: 'org.scalafx', name: 'scalafx_2.12', version: '8.0.144-R12'

    compile group: 'io.spray', name: 'spray-json_2.12', version: '1.3.4'
    compile group: 'it.unibo.alice.tuprolog', name: 'tuprolog', version: '3.2.1'

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'

    compile group: 'com.typesafe.akka', name: 'akka-actor_2.12', version: '2.5.14'
    compile group: 'com.typesafe.akka', name: 'akka-remote_2.12', version: '2.5.14'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.scalatest', name: 'scalatest_2.12', version: '3.0.5'
    testCompile group: 'org.scalamock', name: 'scalamock-scalatest-support_2.12', version: '3.6.0'
    testCompile group: 'com.typesafe.akka', name: 'akka-testkit_2.12', version: '2.5.14'
}

// running <<gradle test>> runs also scalaTests
test.dependsOn('scalaTests')

// necessary for running scala tests without adding
// @RunWith(classOf[JUnitRunner]) to each test class
task scalaTests(dependsOn: ['testClasses'], type: JavaExec) {
    main = 'org.scalatest.tools.Runner'
    args = ['-R', 'build/classes/scala/test', '-o']
    classpath = sourceSets.test.runtimeClasspath
}

// jar will be saved to build/libs
task fatJar(type: Jar, dependsOn: subprojects.compileJava) {
    manifest {
        attributes 'Implementation-Title': 'Osmos-Redux',
            'Implementation-Version': rootProject.version,
            'Main-Class': 'it.unibo.osmos.redux.main.AppLauncher'
    }
    baseName = "${rootProject.name}"
    from(configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }) {
        // remove all signature files
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude 'build'
        exclude '.gradle'
        exclude 'build.gradle'
        exclude 'gradle'
        exclude 'gradlew'
        exclude 'gradlew.bat'
    }
    with jar
    doLast {
        println("fatJar created")
    }
}

defaultTasks 'clean', 'build'
